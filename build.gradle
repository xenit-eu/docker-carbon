import eu.xenit.gradle.ImageTagUtils

plugins {
    id 'idea'
    id 'java'
    id "eu.xenit.docker" version "4.1.1"
}

repositories {
    mavenCentral()
}

ext {
    carbonVersion = "0.9.16"
    twistedVersion = "13.2.0"

    dockerRepository = 'hub.xenit.eu/public/carbon'

    junitJupiterVersion = '5.5.2'
    mockitoVersion = '3.1.0'
    hamcrestVersion = '1.3'
    slf4jVersion = '1.7.25'
    testContainersVersion = '1.12.3'
}

dockerFile {
    dockerFile = file('src/main/resources/Dockerfile')
    dockerBuild {
        repository = "${dockerRepository}"
        tags = ImageTagUtils.toTags("${project.carbonVersion}")
        automaticTags = false
    }
}

buildDockerImage {
    buildArgs = [
            'CARBON_VERSION': project.carbonVersion,
            'TWISTED_VERSION': project.twistedVersion
    ]

    doFirst {
        println "-- build args: $buildArgs"
    }
}

/* Integration testing */

jar.enabled = false // The artifact of this project is the docker image, not the jar

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.hamcrest:hamcrest-all:${hamcrestVersion}"

    testImplementation "org.testcontainers:testcontainers:${testContainersVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testContainersVersion}"

    testImplementation group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: "${slf4jVersion}"
}

test {
    dependsOn buildDockerImage

    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }

    environment 'DOCKER_IMAGE', "${dockerRepository}:${carbonVersion}"
}
